#!/usr/bin/perl
# abstract: add results from a beet query to the mpd playlist
# keep in mind that beet and mpd need to be in sync, or else:
# {add} No such directory at /home/scp1/lib/perl5/Audio/MPD.pm line 156
#   '--> error handling below

use strict;
use warnings FATAL => 'all';
use utf8;
use open qw(:std :utf8);

use Encode;
use Try::Tiny;
use Audio::MPD;
use IPC::Cmd qw(run);
use File::LsColor qw(ls_color);
use Term::ExtendedColor qw(fg bold underline);

our $VERSION = '0.10';

my $mpd = Audio::MPD->new;


# ask user for a query if it's not given on the commandline
my $q = undef;
if(!@ARGV) {
  printf "query: ";
  chomp(my $answer = <STDIN>);
  $q = $answer;
}
else {
  $q = join(' ', @ARGV);
}

# make paths relative for mpd
my @tracks = map { $_ =~ s|\Q$ENV{XDG_MUSIC_DIR}\E||; $_ } beet_ls($q);

printf "> adding %s songs matching query '%s'\n",
  fg(208, bold(scalar @tracks)), fg(137, underline($q));

for my $track(@tracks) {
  try {
    $mpd->playlist->add($track);
  }
  catch {
    chomp $_;
    $_ =~ m/No such directory/
      and printf "W: No such directory: $ENV{XDG_MUSIC_DIR}%s\n",
          ls_color($track);
  }
}


sub beet_ls {
  my $query = shift;

  my $buffer;

  scalar run(
    command => "beet ls $query",
    verbose => 0,
    buffer  => \$buffer,
    timeout => 0,
  );
  my @results = split(/\n/, decode_utf8($buffer));
  return @results;
}

=pod

=head1 beet2mpd

A bridge between beets and mpd. Lets you add songs to mpd using the
advanced query syntax system of beets.

=head1 SYNOPSIS

    beet2mpd                             # add everything
    beet2mpd comp:1                      # add compilations
    beet2mpd loved:1                     # https://github.com/trapd00r/utils/blob/master/love
    beet2mpd country:se                  # add music from sweden only
    beet2mpd genre:game                  # add game music
    beet2mpd mbid_trackid::^$            # add everything missing musicbrainz id
    beet2mpd label:'Masters of Hardcore' # add everything from a record label
    beet2mpd comments:foo                # do note the plural form!
    beet2mpd ^genre:rap albumtype:single # single releases that's NOT rap

    # add official album releases
    beet2mpd albumtype:album albumstatus:official

    # add music released the day you were born
    beet2mpd original_day:14 original_month:8 original_year:1990

    # add music from year 2000-2004 that's NOT pop, rock, rap or hip-hop:
    beet2mpd year:2000..2004 \^genre:pop,rock,rap,hip-hop

    # add music from 1999 where album name contains love, but track
    # titles does not:
    beet2mpd  year:1999 album:love \^title:love

=head1 ENVIRONMENT

B<${XDG_MUSIC_DIR}> should be set to the same directory as your library in
I<beets/config.yaml>

Keep in mind that beets and mpd should be in sync, or else:

  {add} No such directory at /home/scp1/lib/perl5/Audio/MPD.pm line 156

Though accounted for in the error handling, this will mean that some
results from a given query won't be added to the playlist.

=head1 INSTALLATION

=head2 cpanminus

    $ cd beet2mpd/
    $ cpanm .

=head2 Manual method

    $ cd beet2mpd/
    $ cpan Audio::MPD File::LsColor Try::Tiny
    $ perl Makefile.PL
    $ make
    # make install


=head1 AUTHOR

  Magnus Woldrich
  CPAN ID: WOLDRICH
  m@japh.se
  japh@irc.libera.chat
  http://japh.se
  http://github.com/trapd00r

=head1 COPYRIGHT

Copyright 2021 Magnus Woldrich
